<программа>::=program <имя>;<раздел переменных><раздел операторов>
<имя>::=<идентификатор>
<раздел операторов>::= begin <оператор>{;<оператор>} end.
<оператор>::=<оператор присваивания> | <оператор вывода> | <составной оператор> | <спец. оператор>
<оператор присваивания>::= <имя>:=<выражение>
<выражение>::= <простое выражение> | <спец. выражение><операция отношения><спец. выражение>
<операция отношения>::= > | < | <> | <= | >= | =
<простое выражение>::= <терм>{<операция сложения><терм>}
<терм>::= <атом>{<операция умножения><атом>}
<атом>::= <имя> | (<выражение>) | <спец. атом>
<оператор вывода>::= writeln(<список элементов>)
<список элементов>::= <элемент>{,<элемент>}
<составной оператор>::= begin <оператор>{; <оператор>} end
<знак>::= <пусто> | + | –
<конст>::= <знак> <цифра> {<цифра>} | <цифра> {<цифра>}.<цифра> {<цифра>}
<конст. бз>::= <цифра> {<цифра>} | <цифра> {<цифра>}.<цифра> {<цифра>}
<логич. значение>::= true | false
<конст. перечислимого типа>::= <имя>
<раздел переменных>::= var <секция1> {; <секция1>};
<секция1>::= <имя> {, <имя>} : <тип>
<тип>::= integer | boolean | real
<операция сложения>::= + | - | or
<операция умножения>::= * | div | and
<спец. выражение>::= <имя> | <знак> <конст> | <логич. значение>
<спец. атом>::= <конст. бз> | <логич. значение> | not <атом>
<элемент>::= <выражение>
<спец. оператор>::= <оператор выбора> | <оператор цикла с постусловием>
<оператор цикла с постусловием>::= do <оператор> while (<выражение>)
<оператор выбора>::= case <выражение> of <вариант> {; <вариант>} endcase
<вариант>::= <метка варианта> : <оператор>
<метка варианта>::= <конст> | <логическое значение> | <конст. перечислимого типа>
